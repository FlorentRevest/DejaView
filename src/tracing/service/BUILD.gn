# Copyright (C) 2024 The Android Open Source Project
#
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#      http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("../../../gn/fuzzer.gni")
import("../../../gn/dejaview.gni")
import("../../../gn/test.gni")

source_set("service") {
  public_deps = [
    "..:common",
    "../../../include/dejaview/ext/base",
    "../../../include/dejaview/ext/tracing/core",
  ]
  deps = [
    "../../../gn:default_deps",
    "../../../include/dejaview/tracing",
    "../../../protos/dejaview/common:zero",
    "../../../protos/dejaview/config:zero",
    "../../../protos/dejaview/trace:zero",
    "../../../protos/dejaview/trace/dejaview:zero",  # For MetatraceWriter.
    "../../base",
    "../../base:version",
    "../../protozero/filtering:message_filter",
    "../../protozero/filtering:string_filter",
    "../core",
  ]
  sources = [
    "histogram.h",
    "metatrace_writer.cc",
    "metatrace_writer.h",
    "packet_stream_validator.cc",
    "packet_stream_validator.h",
    "trace_buffer.cc",
    "trace_buffer.h",
    "tracing_service_impl.cc",
    "tracing_service_impl.h",
  ]
}

if (enable_dejaview_zlib) {
  source_set("zlib_compressor") {
    deps = [
      "../../../gn:default_deps",
      "../../../gn:zlib",
      "../../../include/dejaview/tracing",
      "../core",
    ]
    sources = [
      "zlib_compressor.cc",
      "zlib_compressor.h",
    ]
  }
}

dejaview_unittest_source_set("unittests") {
  testonly = true
  deps = [
    ":service",
    "../../../gn:default_deps",
    "../../../gn:gtest_and_gmock",
    "../../../protos/dejaview/trace:cpp",
    "../../../protos/dejaview/trace:zero",
    "../../../protos/dejaview/trace/dejaview:cpp",
    "../../../src/protozero/filtering:bytecode_generator",
    "../../base",
    "../../base:test_support",
    "../core",
    "../test:test_support",
  ]

  if (enable_dejaview_zlib) {
    deps += [
      ":zlib_compressor",
      "../../../gn:zlib",
    ]
  }

  sources = [
    "histogram_unittest.cc",
    "packet_stream_validator_unittest.cc",
    "trace_buffer_unittest.cc",
  ]

  if (enable_dejaview_zlib) {
    sources += [ "zlib_compressor_unittest.cc" ]
  }

  # These tests rely on test_task_runner.h which
  # has no Windows implementation.
  if (!is_win) {
    sources += [ "tracing_service_impl_unittest.cc" ]
  }
}

dejaview_fuzzer_test("packet_stream_validator_fuzzer") {
  sources = [ "packet_stream_validator_fuzzer.cc" ]
  deps = [
    ":service",
    "../../../gn:default_deps",
  ]
}
