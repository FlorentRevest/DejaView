# Copyright (C) 2018 The Android Open Source Project
#
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#      http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("../../../gn/dejaview.gni")
import("../../../gn/dejaview_cc_proto_descriptor.gni")
import("../../../gn/test.gni")

assert(enable_dejaview_trace_processor_sqlite)

dejaview_cc_proto_descriptor("gen_cc_metrics_descriptor") {
  descriptor_name = "metrics.descriptor"
  descriptor_target = "../../../protos/dejaview/metrics:descriptor"
}

dejaview_cc_proto_descriptor("gen_cc_all_chrome_metrics_descriptor") {
  descriptor_name = "all_chrome_metrics.descriptor"
  descriptor_target = "../../../protos/dejaview/metrics/chrome:descriptor"
}

dejaview_cc_proto_descriptor("gen_cc_all_webview_metrics_descriptor") {
  descriptor_name = "all_webview_metrics.descriptor"
  descriptor_target = "../../../protos/dejaview/metrics/webview:descriptor"
}

source_set("metrics") {
  sources = [
    "metrics.cc",
    "metrics.h",
  ]
  deps = [
    "..:metatrace",
    "../../../gn:default_deps",
    "../../../gn:sqlite",
    "../../../include/dejaview/trace_processor",
    "../../../protos/dejaview/common:zero",
    "../../../protos/dejaview/trace_processor:metrics_impl_zero",
    "../../../protos/dejaview/trace_processor:zero",
    "../../base",
    "../../protozero:protozero",
    "../dejaview_sql/engine",
    "../dejaview_sql/intrinsics/functions",
    "../sqlite",
  ]
  public_deps = [
    ":gen_cc_all_chrome_metrics_descriptor",
    ":gen_cc_all_webview_metrics_descriptor",
    ":gen_cc_metrics_descriptor",
    "../util",
    "../util:descriptors",
    "sql:gen_amalgamated_sql_metrics",
  ]
}

dejaview_unittest_source_set("unittests") {
  testonly = true
  sources = [ "metrics_unittest.cc" ]
  deps = [
    ":metrics",
    "..:lib",
    "../../../gn:default_deps",
    "../../../gn:gtest_and_gmock",
    "../../../gn:sqlite",
    "../../../protos/dejaview/common:zero",
    "../../../protos/dejaview/trace_processor:metrics_impl_zero",
    "../../../protos/dejaview/trace_processor:zero",
    "../../base:test_support",
    "../../protozero",
  ]
}
