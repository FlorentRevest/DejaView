# Copyright (C) 2018 The Android Open Source Project
#
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#      http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("../gn/fuzzer.gni")
import("../gn/dejaview.gni")

if (!build_with_chromium && enable_dejaview_integration_tests) {
  source_set("dejaview_end_to_end_integrationtests") {
    testonly = true
    deps = [
      ":test_helper",
      "../gn:default_deps",
      "../gn:gtest_and_gmock",
      "../include/dejaview/ext/ipc",
      "../include/dejaview/ext/traced",
      "../include/dejaview/ext/tracing/core:test_support",
      "../include/dejaview/protozero",
      "../protos/dejaview/config:cpp",
      "../protos/dejaview/config:zero",
      "../protos/dejaview/config/power:zero",
      "../protos/dejaview/config/sys_stats:cpp",
      "../protos/dejaview/trace:cpp",
      "../protos/dejaview/trace:zero",
      "../protos/dejaview/trace/ftrace:cpp",
      "../protos/dejaview/trace/dejaview:cpp",
      "../protos/dejaview/trace/power:cpp",
      "../protos/dejaview/trace/sys_stats:cpp",
      "../src/base:base",
      "../src/base:test_support",
      "../src/dejaview_cmd:bugreport_path",
      "../src/protozero/filtering:bytecode_generator",
    ]
    if (enable_dejaview_traced_probes) {
      deps += [
        "../src/traced/probes/ftrace",
        "../src/traced/probes/ftrace:ftrace_procfs",
      ]
    }

    # These binaries are requires by the cmdline tests, which invoke dejaview
    # and trigger_dejaview via Subprocess.
    data_deps = [
      "../src/dejaview_cmd:dejaview",
      "../src/dejaview_cmd:trigger_dejaview",
    ]

    sources = [
      "android_integrationtest.cc",
      "cmdline_integrationtest.cc",
      "ftrace_integrationtest.cc",
      "traced_integrationtest.cc",
    ]
    if (start_daemons_for_testing) {
      # In CTS mode we use /syste/bin/dejaview for the cmdline tests and the
      # dejaview_cmd is not required. Outside of CTS mode, instead, we need to
      # build the cmdline code as part of the test executable.
      deps += [
        "../src/dejaview_cmd",
        "../src/dejaview_cmd:trigger_dejaview_cmd",
      ]
    }

    if (enable_dejaview_trace_processor &&
        enable_dejaview_trace_processor_sqlite &&
        !dejaview_build_with_android && !is_android) {
      sources += [ "trace_processor_shell_integrationtest.cc" ]
      data_deps += [ "../src/trace_processor:trace_processor_shell" ]
      deps += [ "../protos/dejaview/trace_processor:cpp" ]
    }
  }

  executable("client_api_example") {
    sources = [ "client_api_example.cc" ]
    deps = [
      "..:libdejaview_client_experimental",
      "../gn:default_deps",
      "../include/dejaview/tracing",
      "../protos/dejaview/config/gpu:zero",
      "../protos/dejaview/trace:zero",
      "../protos/dejaview/trace/gpu:zero",
    ]
  }
}  # if (!build_with_chromium && enable_dejaview_integration_tests)

dejaview_fuzzer_test("end_to_end_shared_memory_fuzzer") {
  sources = [ "end_to_end_shared_memory_fuzzer.cc" ]
  testonly = true
  deps = [
    ":test_helper",
    "../gn:default_deps",
    "../protos/dejaview/trace:zero",
    "../src/base:test_support",
    "../src/protozero",
    "../src/tracing/core",
    "../src/tracing/ipc/producer",
    "../src/tracing/ipc/service",
  ]
}

dejaview_fuzzer_test("producer_socket_fuzzer") {
  sources = [ "producer_socket_fuzzer.cc" ]
  testonly = true
  deps = [
    ":test_helper",
    "../gn:default_deps",
    "../protos/dejaview/trace:zero",
    "../src/base:test_support",
  ]
}

# dejaview_fuzzer_test() targets are no-ops if is_fuzzer = false.
if (enable_dejaview_benchmarks || is_fuzzer ||
    enable_dejaview_integration_tests) {
  source_set("test_helper") {
    testonly = true
    public_deps = [
      "../protos/dejaview/trace:cpp",
      "../src/tracing/ipc/consumer",
      "../src/tracing/ipc/producer",
      "../src/tracing/ipc/service",
    ]
    deps = [
      "../gn:default_deps",
      "../protos/dejaview/config:cpp",
      "../protos/dejaview/trace:zero",
      "../src/base:test_support",
      "../src/ipc:dejaview_ipc",
      "../src/tracing/ipc:common",
    ]
    sources = [
      "fake_producer.cc",
      "fake_producer.h",
      "test_helper.cc",
      "test_helper.h",
    ]
    if (is_android) {
      sources += [
        "android_test_utils.cc",
        "android_test_utils.h",
      ]
    }
    if (!build_with_chromium && enable_dejaview_traced_probes) {
      deps += [
        "../include/dejaview/ext/traced",
        "../src/traced/probes:probes_src",
      ]
    }
  }
}

if (enable_dejaview_integration_tests) {
  source_set("integrationtest_initializer") {
    testonly = true
    deps = [ "../gn:default_deps" ]
    sources = [ "integrationtest_initializer.h" ]
  }
  source_set("integrationtest_main") {
    testonly = true
    deps = [
      ":integrationtest_initializer",
      "../src/base",
      "../gn:default_deps",
      "../gn:gtest_and_gmock",
    ]
    sources = [ "integrationtest_main.cc" ]
  }
}

if (enable_dejaview_benchmarks) {
  source_set("end_to_end_benchmarks") {
    testonly = true
    deps = [
      ":test_helper",
      "../gn:benchmark",
      "../gn:default_deps",
      "../gn:gtest_and_gmock",
      "../include/dejaview/ext/traced",
      "../protos/dejaview/config:cpp",
      "../protos/dejaview/trace:cpp",
      "../protos/dejaview/trace:zero",
      "../src/base:test_support",
    ]
    sources = [ "end_to_end_benchmark.cc" ]
  }

  source_set("benchmark_main") {
    testonly = true
    deps = [
      "../gn:benchmark",
      "../gn:default_deps",
    ]
    sources = [ "benchmark_main.cc" ]
  }
}  # if (enable_dejaview_benchmarks)

if (dejaview_build_with_android || (is_android && dejaview_build_standalone)) {
  # This is used only in-tree builds. It's built in standalone builds just to
  # get build coverage.
  static_library("dejaview_gtest_logcat_printer") {
    testonly = true
    complete_static_lib = true
    sources = [ "gtest_logcat_printer.cc" ]
    deps = [
      "../gn:default_deps",
      "../gn:gtest_and_gmock",
    ]
  }
}
